| http://mqemulator.net/forum2/viewtopic.php?t=1551
| http://mqemulator.net/forum2/viewtopic.php?t=794

#include includes\spell_routines.inc
#include includes\ini.inc
#include includes\common.inc
#include includes\commonCaster.inc
#include includes\commonBuffer.inc
#include includes\commonNuke.inc
#include includes\wait4rez.inc
#turbo 40

Sub Main
    /declare wizardIniFileName string local "ini/${MacroQuest.Server}/Wizard_${Me.CleanName}.ini"

    /call InitWizardIni ${wizardIniFileName}
    /if (${Macro.Return} == 0) {
        /echo "Bootstrapped INI file, please update variables in: ${wizardIniFileName}"
        /endmacro
    }

    /echo "Wizard all fired up!"
    /bca "Wizard all fired up!"
    :WizardLoop
        /call Wait4Rez_Background_Events
        /if (${Me.Invis}) /goto :WizardLoop
        /if (${Stick.Status.Equal[ON]}) /goto :WizardLoop

        /call CheckManaStatus
        /call DoHarvest
        /call TryMedidate
        /call DoBuffs
        /call DoNuke
        /call DoCommonEvents
        /goto :WizardLoop
/return

Sub DoHarvest
    /if (${Debug} || ${Debug_Wizard}) /echo |- DoHarvest ==>
    
    /if (${autoHarvest} && ${Defined[harvestSpell]} && ${Cast.Ready[${harvestSpell}]} && ${Me.Gem[${harvestSpell}]}) {
        /call CheckNPCInRange
        /declare hasNpcInCamp   bool    local ${Bool[${Macro.Return}]}  
        /declare manaFromMax    int     local ${Math.Calc[${Me.MaxMana}-${Me.CurrentMana}]}

        /if ((!${hasNpcInCamp} && ${manaFromMax} > 350) || ${Me.PctMana} < 2) {
            /call cast "${harvestSpell}"
        }
    }

    /if (${Debug} || ${Debug_Wizard}) /echo <== DoHarvest -|
/return

Sub InitWizardIni(string iniFileName)
    /declare wizardOptions    string  outer "WizardOptions"
    /declare parentInitResult int     local 1
    /declare autoHarvest      bool    outer FALSE
    /declare harvestSpell     string  outer 
    /if (!${Defined[Debug_Wizard]})           /declare Debug_Wizard           bool    outer FALSE

    /call InitGenralIni "${iniFileName}"
    /varset parentInitResult ${Macro.Return}

    /call InitNukeIni "${iniFileName}"
    /if (${Macro.Return} == 0) {
        /varset parentInitResult 0
    }

    /call InitBufferIni "${iniFileName}"
    /if (${Macro.Return} == 0) {
        /varset parentInitResult 0
    }
    
    /call InitWait4Rez "${iniFileName}"
    /if (${Macro.Return} == 0) {
        /varset parentInitResult 0
    }

    /if (${Ini[${iniFileName},${wizardOptions},AutoHarvest].Length}) {
       /varset autoHarvest ${Bool[${Ini[${iniFileName},${wizardOptions},AutoHarvest]}]}
    } else {
        /ini "${iniFileName}" "${wizardOptions}" "AutoHarvest" "${autoHarvest}"
    }

    /if (${Ini[${iniFileName},${wizardOptions},HarvestSpell].Length}) {
       /varset harvestSpell "${Ini[${iniFileName},${wizardOptions},HarvestSpell]}"   
    } else {
        /ini "${iniFileName}" "${wizardOptions}" "HarvestSpell" "${harvestSpell}"
    }
    
    /if (${Ini[${iniFileName},Debug,Debug_Wizard].Length}) {
       /varset Debug_Wizard ${Bool[${Ini[${iniFileName},Debug,Debug_Wizard]}]}     
    } 

/return ${parentInitResult}
