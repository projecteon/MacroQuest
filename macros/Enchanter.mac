| http://mqemulator.net/forum2/viewtopic.php?t=1551
| http://mqemulator.net/forum2/viewtopic.php?t=794

#include includes\spell_routines.inc
#include includes\ini.inc
#include includes\common.inc
#include includes\commonCaster.inc
#include includes\commonBuffer.inc
#include includes\commonDebuffer.inc
#include includes\commonNuke.inc
#include includes\wait4rez.inc
#turbo 40

Sub Main
    /declare enchanterIniFileName   string  local "ini/${MacroQuest.Server}/Enchanter_${Me.CleanName}.ini"
    /declare autoMezz               int     outer 1
    /declare mezzRadius             int     outer 100
    /declare mezzSpell              string  outer "Dazzle"

    /call InitEnchanterIni ${enchanterIniFileName}
    /if (${Macro.Return} == 0) {
        /echo "Bootstrapped INI file, please update variables in: ${enchanterIniFileName}"
        /endmacro
    }

    /declare stayInMezzDebuffLoop bool local
    
    /echo "Enchanter all mezzed up!"
    /bca "Enchanter all mezzed up!"
    :EnchanterLoop
        /call Wait4Rez_Background_Events
        /if (${Me.Invis}) /goto :EnchanterLoop
        
        /call DoMezz
        /call DoDeBuffs        
        | /varset stayInMezzDebuffLoop ${Bool[${Macro.Return}]}
        | /if (${stayInMezzDebuffLoop}) /goto :EnchanterLoop
        /call CheckManaStatus
        /call TryMedidate
        /call DoNuke
        /call DoBuffs
        /call DoCommonEvents
        /goto :EnchanterLoop
/return

sub DoMezz
    /if (${Debug}) /echo |- DoMezz ==>
    /if (${autoMezz}) {
        /declare retry int local
        /declare i int local 0
        /declare mezTarg int local
        /declare maTargetId int local ${NetBots[${mainAssist}].TargetID}

        /if (!${SpawnCount[npc los targetable radius ${mezzRadius}]} || ${Me.CurrentMana} < ${Spell[${mezzSpell}].Mana}) /return

        /if (${Debug}) /echo |- DoMezz - # of mezz targets: ${SpawnCount[npc los targetable radius ${mezzRadius}]}
        /for i 1 to ${SpawnCount[npc los targetable radius ${mezzRadius}]}
            /varset mezTarg ${NearestSpawn[${i},npc los targetable radius ${mezzRadius}].ID}
            /if (${Debug}) /echo |- DoMezz - mezz target: ${mezTarg}
            /if (${mezTarg}!=${maTargetId}) {
                /target id ${mezTarg}
                /delay 2s ${Target.ID}==${mezTarg}
                /if (!${Target.ID} || ${Target.ID}!=${mezTarg}) {
                    /if (${Debug}) /echo <== DoMezz -| - wrong target
                    /return
                }

                /if (!${Cast.Ready[${mezzSpell}]}) /delay 3s ${Cast.Ready[${mezzSpell}]}

                /if (${SpellTimer[${mezTarg},any,${mezzSpell}]} < 8 && ${Cast.Ready[${mezzSpell}]} && ${Target.Distance} < ${Spell[${mezzSpell}].Range}) {
                    /call cast "${mezzSpell}" gem${Me.Gem[${mezzSpell}]} 0 CancelMezz 3
                    /bca ${Target.Name} " mezzed!"
                }
            }
            /next i
    }
    /if (${Debug}) /echo <== DoMezz -|
/return


Sub CancelMezz
	/if (${Target.Type.Equal[Corpse]}) /call Interrupt
	/if (${Target.ID} == ${NetBots[${mainAssist}].TargetID}) /call Interrupt
/return

Sub InitEnchanterIni(string iniFileName)
    /declare enchanterOptions   string  outer "EnchanterOptions"
    /declare parentInitResult   int     local 1

    /if (${Ini[${iniFileName},${enchanterOptions},AutoMezz].Length}) {
       /varset autoMezz ${Ini[${iniFileName},${enchanterOptions},AutoMezz]}     
    } else {
        /ini "${iniFileName}" "${enchanterOptions}" "AutoMezz" "${autoMezz}"
    }

    /if (${Ini[${iniFileName},${enchanterOptions},AutoMezzRadius].Length}) {
       /varset mezzRadius ${Ini[${iniFileName},${enchanterOptions},AutoMezzRadius]}     
    } else {
        /ini "${iniFileName}" "${enchanterOptions}" "AutoMezzRadius" "${mezzRadius}"
    }

    /if (${Ini[${iniFileName},${enchanterOptions},AutoMezzSpell].Length}) {
       /varset mezzSpell "${Ini[${iniFileName},${enchanterOptions},AutoMezzSpell]}"   
    } else {
        /ini "${iniFileName}" "${enchanterOptions}" "AutoMezzSpell" "${mezzSpell}"
    }

    /call InitGenralIni "${iniFileName}"
    /varset parentInitResult ${Macro.Return}

    /call InitCasterIni "${iniFileName}"
    /if (${Macro.Return} == 0) {
        /varset parentInitResult 0
    }

    /call InitNukeIni "${iniFileName}"
    /if (${Macro.Return} == 0) {
        /varset parentInitResult 0
    }

    /call InitBufferIni "${iniFileName}"
    /if (${Macro.Return} == 0) {
        /varset parentInitResult 0
    }

    /call InitDebufferIni "${iniFileName}"
    /if (${Macro.Return} == 0) {
        /varset parentInitResult 0
    }
    
    /call InitWait4Rez "${iniFileName}"
    /if (${Macro.Return} == 0) {
        /varset parentInitResult 0
    }

/return ${parentInitResult}