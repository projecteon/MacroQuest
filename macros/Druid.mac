#include includes\spell_routines.inc
#include includes\ini.inc
#include includes\common.inc
#include includes\commonSpell.inc
#include includes\commonCaster.inc
#include includes\commonHealer.inc
#include includes\commonBuffer.inc
#include includes\commonDebuffer.inc
#include includes\commonNuke.inc
#include includes\wait4rez.inc
#turbo 150

Sub Main
    /declare druidIniFileName        string  local "ini/${MacroQuest.Server}/Druid_${Me.CleanName}.ini"

    /call InitDruidIni ${druidIniFileName}
    /if (${Macro.Return} == 0) {
        /echo "Bootstrapped INI file, please update variables in: ${druidIniFileName}"
        /endmacro
    }

    /echo "Druid sow'ed up!"
    /bca "Druid sow'ed up!"

    :DruidLoop
        /call Wait4Rez_Background_Events
        /if (${Me.Invis}) /goto :DruidLoop
        /if (${Stick.Status.Equal[ON]}) /goto :DruidLoop
        
        /call CheckHeals
        /if (${Macro.Return} == 1) /goto :DruidLoop
        /call CheckManaStatus
        /call TryMedidate
        /call DoDeBuffs 
        /call DoNuke
        /call DoBuffs
        /call DoCommonEvents
        /goto :DruidLoop
/return

Sub InitDruidIni(string iniFileName)
    /declare parentInitResult    int     local 1

    /call InitGenralIni "${iniFileName}"
    /varset parentInitResult ${Macro.Return}
        
    /call InitHealIni "${iniFileName}"
    /if (${Macro.Return} == 0) {
        /varset parentInitResult 0
    }
    
    /call InitBufferIni "${iniFileName}"
    /if (${Macro.Return} == 0) {
        /varset parentInitResult 0
    }
    
    /call InitDebufferIni "${iniFileName}"
    /if (${Macro.Return} == 0) {
        /varset parentInitResult 0
    }
    
    /call InitNukeIni "${iniFileName}"
    /if (${Macro.Return} == 0) {
        /varset parentInitResult 0
    }
    
    /call InitWait4Rez "${iniFileName}"
    /if (${Macro.Return} == 0) {
        /varset parentInitResult 0
    }

/return ${parentInitResult}