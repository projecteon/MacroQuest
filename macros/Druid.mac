#include includes\spell_routines.inc
#include includes\common.inc
#include includes\commonCaster.inc
#include includes\commonHealer.inc
#include includes\commonBuffer.inc
#include includes\commonNuke.inc
#turbo 40

Sub Main
    /declare druidIniFileName        string  local "ini/${MacroQuest.Server}/Druid_${Me.CleanName}.ini"

    /call InitDruidIni ${druidIniFileName}
    /if (${Macro.Return} == 0) {
        /echo "Bootstrapped INI file, please update variables in: ${druidIniFileName}"
        /endmacro
    }

    /echo "Druid sow'ed up!"
    /bca "Druid sow'ed up!"
    :DruidLoop
        /call CheckHeals
        /if (${Macro.Return} == 1) /goto :DruidLoop
        /call CheckManaStatus
        /call TryMedidate
        /call DoNuke
        /call DoBuffs
        /goto :DruidLoop
/return

Sub InitDruidIni(string iniFileName)
    /declare bootstrapInitResult    int     local 1
        
    /call InitHealIni "${iniFileName}"
    /varset bootstrapInitResult ${Macro.Return}
    
    /call InitBufferIni "${iniFileName}"
    /if (${Macro.Return} == 0) {
        /varset bootstrapInitResult 0
    }
    
    /call InitNukeIni "${iniFileName}"
    /if (${Macro.Return} == 0) {
        /varset bootstrapInitResult 0
    }

/return ${bootstrapInitResult}